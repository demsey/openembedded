#@TYPE: Machine
#@NAME: Dreambox DM 7025
#@DESCRIPTION: Machine configuration for the Dreambox DM 7025

PREFERRED_VERSION_linux-dm7025 = "2.6.32"
PREFERRED_VERSION_linux-libc-headers = "2.6.32"

EXTRA_IMAGEDEPENDS += "\
	klibc squashfs-tools-native \
	dreambox-boottool \
"

#needed for wpa-supplicant
COMBINED_FEATURES_append = "nl80211"

#JAVA_mipsel = ",java"

#take care when you do changes on MACHINE_ESSENTIAL_EXTRA_RDEPENDS/RRECOMMENDS you have to increment the recipes/tasks/task-boot.bb PR

MACHINE_ESSENTIAL_EXTRA_RDEPENDS = "\
	kernel-module-pata-xilleon \
	kernel-module-nls-iso8859-15 \
	kernel-module-nls-iso8859-1 \
	kernel-module-nls-cp437 \
	kernel-module-nls-cp850 \
	kernel-module-nls-utf8 \
	kernel-module-squashfs \
	kernel-module-unionfs \
	kernel-module-loop \
	dreambox-boottool \
"

MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS = "\
	kernel-module-cifs \
	kernel-module-exportfs \
	kernel-module-ext2 \
	kernel-module-reiserfs \
	kernel-module-xfs \
"

KV = "${PREFERRED_VERSION_linux-dm7025}-1.3-dm7025"

EXTRA_IMAGECMD_jffs2 = " --eraseblock=0x4000 -n -l "
IMAGE_CMD_jffs2 = "\
	install -d ${DEPLOY_DIR_IMAGE}/boot_tmp; \
	for i in bin dev mnt/flash mnt/squashfs mnt/root; \
	do \
		install -d ${IMAGE_ROOTFS}/boot/$i; \
	done; \
	install -d ${IMAGE_ROOTFS}/boot/lib/modules/${KV}/kernel/fs; \
	install -d ${IMAGE_ROOTFS}/boot/lib/modules/${KV}/kernel/drivers/block; \
	install -d ${IMAGE_ROOTFS}/boot/lib/modules/${KV}/kernel/lib; \
	mv ${IMAGE_ROOTFS}/lib/modules/${KV}/kernel/fs/squashfs \
		${IMAGE_ROOTFS}/boot/lib/modules/${KV}/kernel/fs; \
	mv ${IMAGE_ROOTFS}/lib/modules/${KV}/kernel/fs/unionfs \
		${IMAGE_ROOTFS}/boot/lib/modules/${KV}/kernel/fs; \
	mv ${IMAGE_ROOTFS}/lib/modules/${KV}/kernel/drivers/block/loop.ko \
		${IMAGE_ROOTFS}/boot/lib/modules/${KV}/kernel/drivers/block; \
	mv ${IMAGE_ROOTFS}/lib/modules/${KV}/kernel/lib/decompress_unlzma.ko \
		${IMAGE_ROOTFS}/boot/lib/modules/${KV}/kernel/lib; \
	mknod -m 660 ${IMAGE_ROOTFS}/boot/dev/console c 5 1; \
	mknod -m 660 ${IMAGE_ROOTFS}/boot/dev/null c 1 3; \
	mknod -m 660 ${IMAGE_ROOTFS}/boot/dev/mtdblock3 b 31 3; \
	mknod -m 660 ${IMAGE_ROOTFS}/boot/dev/loop0 b 7 0; \
	mkfs.jffs2 \
		--root=${IMAGE_ROOTFS}/boot \
		--faketime \
		--disable-compressor=lzo \
		--compression-mode=size \
		--output=${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.boot.jffs2 \
		${EXTRA_IMAGECMD}; \
	rm -rf ${DEPLOY_DIR_IMAGE}/boot_tmp; \
	mv ${IMAGE_ROOTFS}/boot ${DEPLOY_DIR_IMAGE}/boot_tmp; \
	install -d ${IMAGE_ROOTFS}/boot; \
	\
	install -d ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}/delta; \
	cd ${IMAGE_ROOTFS}; \
	for i in `find -maxdepth 3 -name enigma2 | xargs`; \
	do \
		install -d ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}/delta/${i%%/enigma2}; \
		mv $i ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}/delta/${i%%/enigma2}; \
	done; \
	for i in `find -type d -name extra | xargs`; \
	do \
		install -d ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}/delta/${i%%/extra}; \
		mv $i ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}/delta/${i%%/extra}; \
	done; \
	mv usr/bin/gst* ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}/delta/usr/bin; \
	mv usr/lib/*gst* ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}/delta/usr/lib; \
	sed -i 's/^\/dev\/mtdblock2/\/dev\/root/;' ${IMAGE_ROOTFS}/etc/fstab; \
	mksquashfs ${IMAGE_ROOTFS}/* ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}/squashfs \
		-comp lzma -root-owned -noappend; \
	\
	mkfs.jffs2 \
		--root=${DEPLOY_DIR_IMAGE}/${IMAGE_NAME} \
		--faketime \
		--compression-mode=size \
		--output=${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.jffs2 \
		${EXTRA_IMAGECMD}; \
	for i in ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}/delta/*; \ 
		do cp -R $i ${IMAGE_ROOTFS}; done; \
	rm -rf ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}; \
	rm -rf ${IMAGE_ROOTFS}/boot; \
	mv ${DEPLOY_DIR_IMAGE}/boot_tmp ${IMAGE_ROOTFS}/boot; \
	buildimage ${STAGING_LIBDIR}/dreambox-secondstage/main.bin.gz \
		${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.boot.jffs2 \
		${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.jffs2 \
		${MACHINE} \
		> ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.nfi"

MACHINE_FEATURES += "wifi"

require conf/machine/include/dreambox-mipsel.inc
